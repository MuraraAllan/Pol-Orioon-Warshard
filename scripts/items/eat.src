use uo;
use os;
use util;

include "include/canAccess";

const POISON_SOUND := 0x247;
const ACTION_EAT := 0x22;


program eat_food(who, food)
  EraseObjProperty(who, "HealTimer");
  EraseObjProperty(who, "IsMeditating");
  if(food.movable == 0)
    SendSysMessage(who, "You cannot eat that.");
    return;
  endif
  if(!can_access(who, food))
    return;
  endif
  if(!ReserveItem(food))
    return;
  endif
  var poison := CInt(GetObjProperty(food, "poison_level") );
  var hunger := CInt(GetObjProperty(who, "Hunger") );
  if(!hunger)
	hunger := 0;
  endif
  SubtractAmount(food, 1);
  if(hunger == 0)
	SendSysMessage(who, "You are not hungry.");
  elseif(hunger < 9)
	SendSysMessage(who, "That was refreshing!");
	hunger := 0;
  elseif (hunger < 15)
	SendSysMessage(who, "That was good, but you are still hungry.");
	hunger := 6;
  else
	SendSysMessage(who, "You are still very hungry.");
	hunger := 12;
  endif
  SetObjProperty(who, "Hunger", hunger);
  PlaySoundEffect(who, (59 + RandomInt(3) ) );
  PerformAction(who, ACTION_EAT);
  if(poison > 0)
	SendSysMessage(who, "You have been poisoned!");
	var poison_level :=CInt(GetObjProperty(who, "poison_level"));
	if(!poison_level)
	  poison_level := 0;
    endif
	poison_level := poison_level + poison;
	SetObjProperty(who, "poison_level", poison_level);
    SetObjProperty(who, "PoisonStamp", ReadGameClock());
    var attacker := SystemFindObjectBySerial(CInt(GetObjProperty(food, "poisoner")), SYSFIND_SEARCH_OFFLINE_MOBILES);
    if(attacker)
      SetObjProperty(who, "lasthit", attacker.serial);
    endif
    var lasthitparms := {attacker.name, attacker.serial, "poison food"};
    SetObjProperty(who, "LastHit", lasthitparms);
    var parms := { who, attacker, "poison food" }
	start_script(":spells:poisonDamage", parms);
  endif
endprogram