use os;
use uo;

include "saveOpts";
include "util/bank";

while (1)
  sleep( MINUTES_BETWEEN_SAVES * 3 );
  accountcheck();
  syslog( "Saving World State" );
  Broadcast( "Salvando o mundo" );
  var duration := ReadGameClock();
  var res := SaveWorldState();
  duration := ReadGameClock() - duration;
  if (res)
    SetGlobalProperty( "#:saver:lastsave", ReadGameClock() );
    syslog( "Mundo Salvo Em : " + duration + " seconds" );
  else
    Broadcast( "Error saving world state!  Oh, the humanity!" );
    syslog( "Error saving world state: " + res );
  endif
endwhile

function accountcheck()
  var accts := ListAccounts();
  var lasttime;
  var acct, chr, holder;
  var i := 1;
  var rgc := ReadGameClock();
  foreach nme in accts
    acct := FindAccount(nme);
    if(acct)
      lasttime := acct.GetProp("LastLogin");
      if(!lasttime)
        acct.setprop("LastLogin", rgc);
      else
        if((rgc - lasttime) > 2592000)
          i := 1;
          while(i <= 5)
            chr := acct.GetCharacter(i);
            if(chr)
              holder := GetObjProperty(chr, "LastLogin");
              if(holder > lasttime)
                lasttime := holder;
              endif
            endif
            i := i + 1;
          endwhile
          if((rgc - lasttime) > 2592000)
            DeleteAccount(nme, acct);
          endif
        endif
      endif
    endif
  endforeach
endfunction

function DeleteAccount(acctname, act);
  var characters := EnumerateOnlineCharacters();
  print("account " + acctname + " has been banned for inactivity.");
  act.ban();
  var chr, mt, ms;
  var i := 1;
  while(i <= 5)
    chr := act.GetCharacter(i);
    if(chr)
      WipeBankBox(chr);
      foreach thing in ListEquippedItems(chr)
        if(thing != chr.backpack)
          if(thing.objtype == 0xf021)
            ms := GetObjProperty(thing, "serial");
            mt := SystemFindObjectBySerial(ms);
            ApplyRawDamage(mt, 2000);
          endif
          MoveItemToContainer(thing, chr.backpack);
        endif
      endforeach
      destroyitem(chr.backpack);
      act.DeleteCharacter(i);
    endif
    i := i + 1;
  endwhile
endfunction